plugins {
    id 'java'
    id 'scala'
    id 'com.github.maiflai.scalatest' version '0.32'
}

group 'com.github.maizy'
version '0.1-SNAPSHOT'
sourceCompatibility = 11

ext {
    scalaMajorVersion = '2.12'
    scalaMinorVersion = '15'
    scalaVersion = "${scalaMajorVersion}.${scalaMinorVersion}"
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'com.github.maiflai.scalatest'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.scala-lang:scala-library:${scalaVersion}"
        implementation "org.scala-lang:scala-reflect:${scalaVersion}"

        implementation "ch.qos.logback:logback-classic:1.2.10"
        implementation "com.typesafe.scala-logging:scala-logging_${scalaMajorVersion}:3.9.4"

        testImplementation "org.scalatest:scalatest_${scalaMajorVersion}:3.2.11"
        testImplementation 'com.vladsch.flexmark:flexmark-all:0.64.0'
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.additionalParameters = [
                '-Xlint:_',
                '-feature',
                '-Xfatal-warnings',
                '-Ypartial-unification',
                '-Ywarn-dead-code',
                '-Ywarn-inaccessible',
                '-Ywarn-infer-any',
                '-Ywarn-nullary-override',
                '-Ywarn-nullary-unit',
                '-Ywarn-numeric-widen'
        ]
    }
}

project('trbrk') {
    apply plugin: 'application'

    application {
        mainClass = 'com.github.maizy.trbrk.app.Main'
    }

    dependencies {
        implementation 'org.knowm.xchart:xchart:3.8.1'
    }
}
